#INCLUDE "MATA456.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATA456  ³ Autor ³ Rosane L. Chene       ³ Data ³ 15.01.92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Liberacao de Credito/Estoque                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void MATA456(void)                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
User Function MATA456CUS()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aArea     := GetArea()
Local cFilSC9   := ""
Local cCondicao := ""
Local cPerg     := "MTA456"
Local aIndSC9   := {}
Local aCores    := {{"C9_BLEST=='  '.And.C9_BLCRED=='  '.And.(C9_BLWMS>='05'.OR.C9_BLWMS='  ').And.Iif(SC9->((FieldPos('C9_BLTMS') > 0)), Empty(C9_BLTMS), .T.)",'ENABLE' },;	//Item Liberado
	{ "(C9_BLCRED =='10' .And. C9_BLEST  == '10').Or.C9_BLCRED =='ZZ' .And. C9_BLEST  == 'ZZ'",'DISABLE'},;		   	//Item Faturado
	{ "!C9_BLCRED=='  '.And. C9_BLCRED <> '09'.And. C9_BLCRED <> '10'.And. C9_BLCRED <> 'ZZ'",'BR_AZUL'},;	//Item Bloqueado - Credito
	{ "C9_BLEST=='XX' .AND. C9_BLCRED <> '09' .And. C9_BLCRED <> '10' .And. C9_BLCRED <> 'ZZ'","BR_PINK"},;//Item Aguardando a expedicao
	{ "!C9_BLEST =='  '.And. C9_BLCRED <> '09'.And. C9_BLEST  <> '10'.And. C9_BLEST  <> 'ZZ'",'BR_PRETO'},;	//Item Bloqueado - Estoque
	{ "C9_BLWMS <='05'  .And. !C9_BLWMS == '  '",'BR_AMARELO'},;	//Item Bloqueado - WMS
	{ "C9_BLCRED == '09' .And. C9_BLCRED <> '10'.And. C9_BLCRED <> 'ZZ'",'BR_MARROM'},; //Item Rejeitado
	{ "Iif(SC9->((FieldPos('C9_BLTMS') > 0)), !Empty(C9_BLTMS), .F.)"  ,'BR_LARANJA'}}	//Item Bloqueado - TMS
	
	
Private bFiltraBrw := {|| Nil} 
If ( cPaisLoc $ "ARG|POR|EUA" )
	Private aArrayAE:={}
EndIf
Private cCadastro := OemToAnsi(STR0005)		//"Libera‡„o de Cr‚dito / Estoque"
Private aRotina := MenuDef()
	
	If VerSenha(136) .And. VerSenha(137)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ So Ped. Bloqueados   mv_par01          Sim Nao               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Pergunte(cPerg,.T.)
			IF (existblock("M456FIL"))
				cFilSC9 := ExecBlock("M456FIL",.f.,.f.)
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³EXECUTAR CHAMADA DE FUNCAO p/ integracao com sistema de Distribuicao - NAO REMOVER ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If GetMV("MV_FATDIST") == "S" // Apenas quando utilizado pelo modulo de Distribuicao
				D456LibCg(@cFilSC9)
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³EXECUTAR CHAMADA DE FUNCAO p/ integracao com sistema de Distribuicao - NAO REMOVER ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("SC9")
			SC9->( DbSetOrder(1) )
			Do Case
				Case Mv_Par01 == 1 //Somente de Estoque
					cFilSC9   := If(Empty(cFilSC9),".T.",cFilSC9)
					cCondicao := "C9_FILIAL=='"+xFilial("SC9")+"'.And."
					cCondicao += "((C9_BLEST<>'  '.And.C9_BLEST<>'10').Or."
					cCondicao += "(C9_BLCRED<>'  '.And.C9_BLCRED<>'10')).And."
					cCondicao += cFilSC9
				Case Mv_Par01==2 //Sem Restricao
					cCondicao := cFilSC9
				Case Mv_Par01==3 //Somente WMS
					cFilSC9   := If(Empty(cFilSC9),".T.",cFilSC9)
					cCondicao := "C9_FILIAL=='"+xFilial("SC9")+"'.And."
					cCondicao += "C9_BLEST=='  '.And."
					cCondicao += "C9_BLCRED=='  '.And."
					cCondicao += "C9_BLWMS=='03'.And."
					cCondicao += cFilSC9
				OtherWise //Todos os Bloqueios
					cFilSC9   := If(Empty(cFilSC9),".T.",cFilSC9)
					cCondicao := "C9_FILIAL=='"+xFilial("SC9")+"'.And."
					cCondicao += "((C9_BLEST<>'  '.And.C9_BLEST<>'10').Or."
					cCondicao += "(C9_BLCRED<>'  '.And.C9_BLCRED<>'10').Or."
					cCondicao += "C9_BLWMS=='03').And."
					cCondicao += cFilSC9
			EndCase
			If !Empty(cCondicao)
				bFiltraBrw := {|| FilBrowse("SC9",@aIndSC9,@cCondicao) }
				Eval(bFiltraBrw)
			Else
				SC9->(MsSeek(xFilial("SC9")))
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Endereca a funcao de BROWSE                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("SC9")
			If SC9->( Eof() )
				HELP(" ",1,"RECNO")
			Else
				mBrowse( 7, 4,20,74,"SC9",,,,,,aCores) //,,"SC9->C9_BLEST+SC9->C9_BLCRED"
			EndIf
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Restaura a integridade da rotina                                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea("SC9")
		SC9->( RetIndex("SC9") )
		SC9->( DbClearFilter() )
		AEval(aIndSc9,{|x| Ferase(x[1]+OrdBagExt())})
	Else
		HELP(" ",1,"SEMPERM")
	Endif
	RestArea(aArea)
	
Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A450LibAut³ Autor ³ Rosane Luciane Chene  ³ Data ³ 10.01.92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina para gerar liberacoes automaticas de cred. e estoque³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA456                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
User Function A456LibAut(cAlias)
Local nOpca    := 0
Local aSays    := {}
Local aButtons := {}
Local cPerg    := "LIBAT2"

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Utiliza arquivo de liberados para geracao na nota            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea(cAlias)
	DbSetOrder(1)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ mv_par01 Pedido de          ?                                 ³
	//³ mv_par02 Pedido ate         ?                                 ³
	//³ mv_par03 Cliente de         ?                                 ³
	//³ mv_par04 Cliente ate        ?                                 ³
	//³ mv_par05 Dta Liberacao de   ?                                 ³
	//³ mv_par06 Dta Liberacao ate  ?                                 ³
	//³ mv_par07 Quanto ao Estoque  ? Estoque/WMS  WMS                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Pergunte(cPerg,.T.)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Nova forma de criar dialogos para processos Batch            ³
		//³ COMPATIVEL COM PROTHEUS (BOF)                                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		AADD(aSays,OemToAnsi( STR0006 ) )
		AADD(aSays,OemToAnsi( STR0007 ) )
		AADD(aButtons, { 5,.T.,{||Pergunte(cPerg) } } )
		AADD(aButtons, { 1,.T.,{|o| nOpca:= 1, o:oWnd:End() } } )
		AADD(aButtons, { 2,.T.,{|o| o:oWnd:End() }} )
		FormBatch( cCadastro, aSays, aButtons )
		IF nOpcA == 1
			Processa({|lEnd| Ma450Processa(cAlias,.T.,.T.,@lEnd,Nil,MV_PAR07==2)},,,.T.)
		EndIf
	EndIf
	
Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A450LibMan³ Autor ³ Rosane Luciane Chene  ³ Data ³ 10.01.92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina para gerar liberacoes manuais de cred. e estoque    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA456                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
User Function A456LibMan(cAlias)

Local aArea     := GetArea()
Local aAreaSC9  := SC9->(GetArea())

Local cPedido   := ""
Local cAliasSC9 := "SC9"
Local cQuery    := ""

Local nOpcA     := 0
Local nRecSc9   := 0
Local nTotRec   := 0

Local lContinua := .T.
Local lQuery    := .F.
Local lHelp     := .T.

Local bValid    := {|| (cAliasSC9)->(Recno()) <= nTotRec}

//-- Variaveis utilizadas pela funcao wmsexedcf
Private aLibSDB	:= {}
Private aWmsAviso:= {}

	If SC9->C9_BLCRED == "10" .OR. SC9->C9_BLEST == "10"
		HELP(" ",1,"A450NFISCA")
		lContinua := .F.
	EndIf
	
	If SC9->C9_BLCRED == "  " .And. (SC9->C9_BLEST == "  " .OR. SC9->C9_BLEST == 'XX') .And. SC9->C9_BLWMS=="  "
		Help(" ",1,"A450JALIB")
		lContinua := .F.
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Caso os parametros MV_CUSFIFO e MV_FFONLIN estejam habilitados nao sera |
	//|permitida a liberacao manual de estoque.                                |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If SuperGetMv("MV_CUSFIFO",.F.,.F.) .And. SuperGetMv("MV_FFONLIN",.F.,.F.)
		PutHelp ("PA450FIFO",	{"Quando utilizado o Custo Fifo On-Line"	,"não e permitida a liberação manual do "	,"saldo bloqueado por estoque."	},;
								{"When used the Cost Fifo On-Line and"		,"not allowed the manual release of the"	,"balance blocked for supply."	},;
								{"Cuando utilizado el costo Fifo On-Line"	,"no permitido el lanzamiento manual del"	,"saldo bloqueado por estoque."	},;
								.F.)
		Help(" ",1,"A450FIFO")
		lContinua := .F.
	EndIf
	
	DbSelectArea("SC5")
	SC5->( DbSetOrder(1) )
	SC5->( MsSeek(xFilial("SC5")+SC9->C9_PEDIDO) )
	If ( SC5->C5_TIPO$"BD" )
		Help(" ",1,"A450NCRED")
		lContinua := .F.
	EndIf
	
	If lContinua
		dbSelectArea(cAlias)
		nOpcA := a450Tela( @lContinua , .T. , .T. )
		#IFDEF TOP
			IF TcSrvType() == "AS/400"
				dbSelectArea("SC9")
				Set Filter to
				dbSetOrder(1)
				aAreaSC9 := GetArea()
			EndIf
		#ENDIF
		If ( ExistBlock("MTA456P") )
			If ( !Execblock("MTA456P",.F.,.F.,{nOpca}) )
				nOpcA := 0
			EndIf
		EndIf
		Do Case
		Case ( nOpcA == 1 )
			a450Grava(1,.T.,.T.)
			If (Existblock("MTA456I"))
				ExecBlock("MTA456I",.f.,.f.)
			EndIf
		Case ( nOpcA == 3 )
			a450Grava(2,.T.,.T.)
			If (Existblock("MTA456R"))
				ExecBlock("MTA456R",.f.,.f.)
			EndIf
		Case ( nOpcA == 4 )
			cPedido := SC9->C9_PEDIDO
			dbSelectArea("SC9")
			dbSetOrder(1)
			#IFDEF TOP
				If .T.//TcSrvType()<>"AS/400"
					lQuery := .T.
	
					bValid := {|| .T.}
	
					cAliasSC9 := "A450LIBMAN"
					cQuery := "SELECT C9_FILIAL,C9_PEDIDO,C9_BLCRED,R_E_C_N_O_ SC9RECNO "
					cQuery += "FROM "+RetSqlName("SC9")+" SC9 "
					cQuery += "WHERE SC9.C9_FILIAL = '"+xFilial("SC9")+"' AND "
					cQuery += "SC9.C9_PEDIDO = '"+cPedido+"' AND "
					cQuery += "(SC9.C9_BLEST <> '  ' OR "
					cQuery += "SC9.C9_BLCRED <> '  ' ) AND "
					cQuery += "SC9.C9_BLCRED NOT IN('10','09') AND "
					cQuery += "SC9.C9_BLEST <> '10' AND "
					cQuery += "SC9.D_E_L_E_T_ = ' ' "
	
					cQuery := ChangeQuery(cQuery)
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC9,.T.,.T.)
				Else
			#ENDIF
				MsSeek(cFilial+cPedido)
			#IFDEF TOP
				EndIf
			#ENDIF
	
	
			nTotRec := SC9->(LastRec())
	
			While ( !Eof() .And. (cAliasSC9)->C9_FILIAL == xFilial("SC9") .And.;
					(cAliasSC9)->C9_PEDIDO == cPedido .And. Eval(bValid) ) 				
	
				If lQuery
					SC9->(MsGoto((cAliasSC9)->SC9RECNO))
				Else
					(cAliasSC9)->(dbSkip())
					nRecSc9 := (cAliasSC9)->(Recno())
					(cAliasSC9)->(dbSkip(-1))
				Endif
	
				If !( (Empty(SC9->C9_BLCRED) .And. Empty(SC9->C9_BLEST)) .Or.;
						(SC9->C9_BLCRED=="10" .And. SC9->C9_BLEST=="10") .Or.;
						SC9->C9_BLCRED=="09" )
	
					a450Grava(1,.T.,.T.,@lHelp)
	
				EndIf
				IF (Existblock("MTA456I"))
					ExecBlock("MTA456I",.f.,.f.)
				EndIf
	
				If lQuery
					dbSelectArea(cAliasSC9)
					dbSkip()
				Else
					(cAliasSC9)->(MsGoto(nRecSc9))
				Endif
	
			EndDo
			If lQuery
				dbSelectArea(cAliasSC9)
				dbCloseArea()
				dbSelectArea("SC9")
			EndIf
		EndCase
		If ExistBlock("MTA456L")
			Execblock("MTA456L",.F.,.F.,{nOpca})
		EndIf
		//-- Integrado ao wms devera avaliar as regras para convocacao do servico e disponibilizar os 
		//-- registros do SDB para convocacao
		If	IntDL() .And. !Empty(aLibSDB)
			WmsExeDCF('2')
		EndIf
	EndIf
	RestArea(aAreaSC9)
	Eval(bFiltraBrw)
	RestArea(aArea)
	
Return(lContinua)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Marco Bianchi         ³ Data ³01/09/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function MenuDef()
     

Private aRotina := {	{STR0001,"PesqBrw"		,0,1,0,.F.},;	// "Pesquisar"
							{STR0003,"U_A456LibAut"	,0,0,0,NIL},;	// "Autom tica"
							{STR0004,"U_A456LibMan"	,0,0,0,NIL},;	// "Manual"
							{STR0037,"A450Legend"	,0,0,0,.F.}}	// "Legenda"

	If ExistBlock("MTA456MNU")
		ExecBlock("MTA456MNU",.F.,.F.)
	EndIf
	
Return(aRotina)
